# Licensed under the MIT License
# https://github.com/craigahobbs/bare-script/blob/main/LICENSE

include <args.bare>
include <schemaDoc.bare>


# $function: baredocMain
# $group: baredoc.bare
# $doc: The BareScript library documentation application main entry point
# $arg url: The library documentation JSON resource URL
# $arg title: The library title
# $arg menuLinks: Optional array of text/URL menu link tuples
# $arg groupURLs: Optional map of group name to group Markdown content URL ('' is index) or JSON resource URL
async function baredocMain(url, title, menuLinks, groupURLs):
    # Parse arguments
    args = argsParse(baredocArguments)
    group = objectGet(args, 'group')
    name = objectGet(args, 'name')
    single = objectGet(args, 'single')
    url = objectGet(args, 'url', url)

    # Render library JSON documentation page?
    if objectGet(args, 'doc'):
        documentSetTitle('Library')
        markdownPrint(schemaDocMarkdown(baredocTypes, 'Library'))
        return
    endif

    # Fetch and validate the library documentation resource
    groups = {}
    urls = if(systemType(url) == 'array', url, [url])
    for libraryJSON, ixLibrary in systemFetch(urls):
        libraryPart = if(libraryJSON != null, jsonParse(libraryJSON))
        libraryPart = if(libraryPart != null, schemaValidate(baredocTypes, 'Library', libraryPart))
        if libraryPart == null:
            libraryURL = arrayGet(urls, ixLibrary)
            markdownPrint('**Error:** Failed to fetch library documentation resource "' + markdownEscape(libraryURL) + '"')
            return
        endif

        # Group the function documentation
        for function_ in objectGet(libraryPart, 'functions'):
            groupName = objectGet(function_, 'group')
            groupFunctions = objectGet(groups, groupName)
            if groupFunctions == null:
                groupFunctions = []
                objectSet(groups, groupName, groupFunctions)
            endif
            arrayPush(groupFunctions, function_)
        endfor
    endfor

    # Group URLs?
    if groupURLs != null:
        # Fetch the group URLs, if necessary
        if systemType(groupURLs) == 'string':
            groupURLs = jsonParse(systemFetch(groupURLs))
        endif

        # Add an empty group for any missing group URLs
        for groupName in objectKeys(groupURLs):
            if groupName != '' && !objectHas(groups, groupName):
                objectSet(groups, groupName, [])
            endif
        endfor
    endif

    # Sort the group functions
    for groupName in objectKeys(groups):
        groupFunctions = objectGet(groups, groupName)
        arraySort(groupFunctions, baredocFunctionCompare)

        # Function requested by name?
        if name != null:
            for function_ in groupFunctions:
                functionName = objectGet(function_, 'name')
                if functionName == name:
                    # Redirect to the actual URL
                    windowSetLocation( \
                        argsURL(baredocArguments, {'group': objectGet(function_, 'group'), 'name': null}, false, functionName) \
                    )
                    return
                endif
            endfor
        endif
    endfor
    if name != null:
        markdownPrint('', '**Error:** Unknown function "' + markdownEscape(name) + '"')
        return
    endif

    # Fetch the group content URLs
    groupContent = {}
    if groupURLs != null:
        # Fetch the content URLs
        groupURLArray = []
        groupNames = arraySort(objectKeys(groupURLs))
        for groupName in groupNames:
            arrayPush(groupURLArray, objectGet(groupURLs, groupName))
        endfor
        groupContentArray = systemFetch(groupURLArray)

        # Assign the group contents
        for groupName, ixGroupName in groupNames:
            objectSet(groupContent, groupName, arrayGet(groupContentArray, ixGroupName))
        endfor
    endif

    # Render the page
    title = if(title != null && !objectHas(args, 'url'), title, url)
    if group != null:
        baredocGroupPage(args, title, groupURLs, groupContent, groups, group)
    elif single:
        baredocSinglePage(args, title, menuLinks, groupURLs, groupContent, groups)
    else:
        baredocIndexPage(args, title, menuLinks, groupURLs, groupContent, groups)
    endif
endfunction


# The BareScript library documentation application arguments
baredocArguments = argsValidate([ \
    {'name': 'doc', 'type': 'bool', 'default': false}, \
    {'name': 'group'}, \
    {'name': 'name'}, \
    {'name': 'publish', 'type': 'bool', 'default': false}, \
    {'name': 'single', 'type': 'bool', 'default': false}, \
    {'name': 'url', 'global': 'vURL'} \
])


# Helper to compare library function objects by name
function baredocFunctionCompare(f1, f2):
    f1Name = objectGet(f1, 'name')
    f2Name = objectGet(f2, 'name')
    return if(f1Name < f2Name, -1, if(f1Name == f2Name, 0, 1))
endfunction


# Render a library documentation index page
function baredocIndexPage(args, title, menuLinks, groupURLs, groupContent, groups):
    publish = objectGet(args, 'publish')

    # Render the menu and title
    documentSetTitle(title)
    if menuLinks != null && arrayLength(menuLinks) > 0 && !publish:
        menuSpacer = '&nbsp;|&nbsp;'
        for menuLink, ixMenuLink in menuLinks:
            menuLinkText = arrayGet(menuLink, 0)
            menuLinkURL = arrayGet(menuLink, 1)
            markdownPrint(if(ixMenuLink == 0, '', menuSpacer) + '[' + markdownEscape(menuLinkText) + '](' + urlEncode(menuLinkURL) + ')')
        endfor
        markdownPrint('')
    endif
    markdownPrint('# ' + markdownEscape(title))

    # Render the index Markdown, if any
    if objectHas(groupContent, ''):
        rootURL = objectGet(groupURLs, '')
        rootMarkdown = objectGet(groupContent, '')
        markdownPrint('', if(rootMarkdown != null, rootMarkdown, '**Error:** Failed to load "' + markdownEscape(rootURL) + '"'))
    endif

    # Render the single page toggle
    if !publish:
        markdownPrint('', argsLink(baredocArguments, 'Single Page', {'single': true}))
    endif

    # Render the library function index
    markdownPrint('', '## Table of Contents', '')
    for groupName in arraySort(objectKeys(groups)):
        markdownPrint('- ' + argsLink(baredocArguments, groupName, {'group': groupName}, false, argsTopHeaderId))
    endfor
endfunction


# Render the single-page library documentation
function baredocSinglePage(args, title, menuLinks, groupURLs, groupContent, groups):
    publish = objectGet(args, 'publish')

    # Render the menu and title
    documentSetTitle(title)
    if menuLinks != null && arrayLength(menuLinks) > 0 && !publish:
        menuSpacer = '&nbsp;|&nbsp;'
        for menuLink, ixMenuLink in menuLinks:
            menuLinkText = arrayGet(menuLink, 0)
            menuLinkURL = arrayGet(menuLink, 1)
            markdownPrint(if(ixMenuLink == 0, '', menuSpacer) + '[' + markdownEscape(menuLinkText) + '](' + urlEncode(menuLinkURL) + ')')
        endfor
        markdownPrint('')
    endif
    markdownPrint('# ' + markdownEscape(title))

    # Render the index Markdown, if any
    if objectHas(groupContent, ''):
        rootURL = objectGet(groupURLs, '')
        rootMarkdown = objectGet(groupContent, '')
        markdownPrint('', if(rootMarkdown != null, rootMarkdown, '**Error:** Failed to load "' + markdownEscape(rootURL) + '"'))
    endif

    # Render the single page toggle
    if !publish:
        markdownPrint('', argsLink(baredocArguments, 'Multi Page', {'single': false}))
    endif

    # The table of contents
    markdownPrint('', '## Table of Contents', '')
    for groupName in arraySort(objectKeys(groups)):
        markdownPrint('- ' + argsLink(baredocArguments, groupName, null, false, groupName))
    endfor

    # Render the library function index
    for groupName in arraySort(objectKeys(groups)):
        markdownPrint('', '---', '')
        baredocGroupPage(args, title, groupURLs, groupContent, groups, groupName)
    endfor
endfunction


# Render a library documentation group page
function baredocGroupPage(args, title, groupURLs, groupContent, groups, groupName):
    publish = objectGet(args, 'publish')
    single = objectGet(args, 'single')
    baseHeader = if(single, '##', '#')

    if !single:
        # Set the document title
        documentSetTitle(title + ' - ' + groupName)

        # Group exist?
        if !objectHas(groups, groupName):
            markdownPrint('', '**Error:** Unknown group "' + markdownEscape(groupName) + '"')
            return
        endif
    endif

    # Render the menu and title
    if !single:
        markdownPrint(argsLink(baredocArguments, 'Index', {'group': null}), '')
    endif
    markdownPrint(baseHeader + ' ' + markdownEscape(groupName))
    if single && !publish:
        markdownPrint('', argsLink(baredocArguments, 'Back to top', null, false, '_top'))
    endif

    # Render the group Markdown, if any
    if objectHas(groupContent, groupName):
        groupURL = objectGet(groupURLs, groupName)
        groupMarkdown = objectGet(groupContent, groupName)
        markdownPrint('', if(groupMarkdown != null, groupMarkdown, '**Error:** Failed to load "' + markdownEscape(groupURL) + '"'))
    endif

    # Render the group function index
    groupFunctions = objectGet(groups, groupName)
    if arrayLength(groupFunctions) != 0:
        markdownPrint('', baseHeader + '# Function Index', '')
    endif
    for function_, ixFunction in groupFunctions:
        functionName = objectGet(function_, 'name')
        markdownPrint('- ' + argsLink(baredocArguments, functionName, null, false, functionName))
    endfor

    # Render the group function documentation
    for function_ in groupFunctions:
        # Render the function header
        markdownPrint( \
            '', \
            '---', \
            '', \
            baseHeader + '# ' + markdownEscape(objectGet(function_, 'name')), \
            '', \
            objectGet(function_, 'doc') \
        )

        # Render the functions's argument documentation
        markdownPrint('', baseHeader + '## Arguments')
        arguments = objectGet(function_, 'args')
        if arguments != null:
            for argument in arguments:
                markdownPrint('', '**' + markdownEscape(objectGet(argument, 'name')) + ' -**', objectGet(argument, 'doc'))
            endfor
        else:
            markdownPrint('', 'None')
        endif

        # Render the function's return documentation
        markdownPrint('', baseHeader + '## Returns')
        returnDoc = objectGet(function_, 'return')
        markdownPrint('', if(returnDoc != null, returnDoc, 'Nothing'))
    endfor
endfunction


# The library documentation schema
baredocTypes = schemaParse( \
    '# A library documentation model', \
    'struct Library', \
    '', \
    '    # The library functions', \
    '    Function[len > 0] functions', \
    '', \
    '', \
    '# A library function', \
    'struct Function', \
    '', \
    '    # The function name', \
    '    string(len > 0) name', \
    '', \
    '    # The function group (e.g. "Math")', \
    '    string(len > 0) group', \
    '', \
    "    # The function's documentation Markdown lines", \
    '    string[len > 0] doc', \
    '', \
    '    # The function arguments', \
    '    optional FunctionArgument[len > 0] args', \
    '', \
    "    # The function return's documentation Markdown lines", \
    '    optional string[len > 0] return', \
    '', \
    '', \
    '# A function argument', \
    'struct FunctionArgument', \
    '', \
    '    # The argument name', \
    '    string(len > 0) name', \
    '', \
    "    # The argument's documentation Markdown lines", \
    '    string[len > 0] doc' \
)
